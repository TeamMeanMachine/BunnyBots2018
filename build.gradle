import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
    id 'jaci.openrio.gradle.GradleRIO' version '2018.03.06'
}

deploy {
    targets {
        target("roborio", RoboRIO) {
            team = 2471
            addresses << "10.24.71.2"
        }
    }
    artifacts {
        // We still use FRCJavaArtifact since kotlin does respond as a Java build.
        artifact('frcKotlin', FRCJavaArtifact) {
            targets << "roborio"
            debug = false
            debugPort = 8348
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

wpi {
    wpilibVersion = '2018.4.1'
    ntcoreVersion = '4.1.0'
    wpiutilVersion = '3.2.0'

    ctreVersion = '5.7.1.0'
}

dependencies {
    compile wpilib()
    compile ctre()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.0"
    compile 'com.github.juchong:ADIS16448-RoboRIO-Driver:2018.2'
    compile project("meanlib")
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes 'Main-Class': 'org.team2471.bunnybots2018.MainKt'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
